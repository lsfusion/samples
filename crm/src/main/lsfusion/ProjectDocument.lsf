MODULE ProjectDocument;

REQUIRE Project, Time, Employee;

NAMESPACE Project;

CLASS Document 'Документ';
TABLE document (Document);

name 'Наименование' = DATA VARSTRING[200] (Document) CHARWIDTH 20;

customer 'Клиент' = DATA Customer (Document);
nameCustomer 'Клиент' (Document d) = name(customer(d));

CLASS Version 'Версия документа';
TABLE version (Version);

document 'Документ' = DATA Document (Version) NONULL DELETE;

dateTime 'Время' = DATA DATETIME (Version) NONULL;
dateTime (Version v) <- currentDateTime() WHEN SET(v IS Version); 

author 'Автор' = DATA Employee (Version);
nameAuthor 'Автор' (Version v) = name(author(v));
author (Version v) <- currentEmployee() WHEN SET(v IS Version);

file 'Файл' = DATA CUSTOMFILE (Version) NONULL;
open 'Открыть' (Version v) = { open(file(v)); }
load 'Загрузить' (Version v) = { INPUT =file(v) CHANGE; }

lastVersion 'Последняя версия' (Document d) = GROUP LAST Version v ORDER dateTime(v), v BY document(v) MATERIALIZED;

dateTimeLastVersion 'Время последнего изменения' (Document d)= dateTime(lastVersion(d));
fileLastVersion 'Последний файл' (Document d)= file(lastVersion(d));

authorLastVersion 'Автор' (Document d) = author(lastVersion(d));
nameAuthorLastVersion 'Автор' (Document d) = name(authorLastVersion(d));

openLastVersion 'Открыть' (Document d) = { open(fileLastVersion(d)); }
loadLastVersion 'Загрузить' (Document d) = {
    INPUT f = CUSTOMFILE DO {
        NEW v = Version {
            document(v) <- d;
            dateTime(v) <- currentDateTime();
            file(v) <- f;
        }
    }
}
loadLastVersionAndApply 'Загрузить' (Document d) = {
    NEWSESSION {
        loadLastVersion(d);
        APPLY;
    }
}


FORM document 'Документ'
    OBJECTS d = Document PANEL
    PROPERTIES(d) nameCustomer, name, openLastVersion SHOWIF lastVersion(d), loadLastVersion
    ORDER nameCustomer(d)
    
    OBJECTS v = Version
    PROPERTIES(v) dateTime, nameAuthor, open TOOLBAR, load TOOLBAR, DELETE
    FILTERS document(v) == d
    
    EDIT Document OBJECT d
;

DESIGN document {
    OBJECTS {
        NEW lastVersion AFTER BOX(d) {
            caption = 'Последняя версия';
            type = CONTAINERH;
            MOVE PROPERTY(openLastVersion(d));
            MOVE PROPERTY(loadLastVersion(d));
        }
    }
}



EXTEND FORM customer
    OBJECTS d = Document
    PROPERTIES(d) name, dateTimeLastVersion READONLY, nameAuthorLastVersion READONLY
    PROPERTIES(d) openLastVersion TOOLBAR SHOWIF lastVersion(d), loadLastVersion TOOLBAR
    PROPERTIES(d) NEW, DELETE
    FILTERS customer(d) == c
    
    OBJECTS v = Version
    PROPERTIES(v) READONLY dateTime, nameAuthor
    PROPERTIES(v) open TOOLBAR, load TOOLBAR, DELETE
    FILTERS document(v) == d
;

DESIGN customer {
    tabPane {
        NEW documents {
            caption = 'Документы';
            MOVE BOX(d);
            MOVE BOX(v);
        }
    }
}

newDocument 'Новый документ' (Project p) = {
    NEWSESSION {
        NEW d = Document {
            customer(d) <- customer(p);
            SHOW document OBJECTS d = d DOCKED;
        }
    }
}