MODULE InvoiceDashboard;

REQUIRE Invoice, Dashboard, InvoiceWord;

NAMESPACE Invoice;

toInvoice 'Часов' = GROUP SUM hours(TimeEntry e) IF NOT invoice(e) BY project(e); 

FORM invoiceDashboard 'Выставление счетов'

    OBJECTS p = Project
    PROPERTIES(p) READONLY nameCustomer, name, toInvoice
    FILTERS assigned(p, currentEmployee())
    ORDER nameCustomer(p)
    FILTERGROUP toInvoice
        FILTER 'Выставить счет' toInvoice(p) 'F11' DEFAULT
    
    OBJECTS t = TimeEntry
    PROPERTIES(t) READONLY namePractice, nameEmployee, nameTaskType, dateTimeStart, hours BACKGROUND userHours(t), nameTask, nameActivity, dateTimeFinish
    FILTERS project(t) == p,
            NOT inProcess(t),
            NOT invoice(t),
            assigned(project(t), currentEmployee())
    
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY date, series, number, nameCustomer, nameProject, fromDate, toDate, hours, nameAuthor
    PROPERTIES(i) generateWord TOOLBAR
    PROPERTIES(i) NEWSESSION EDIT, DELETE
    FILTERS assigned(project(i), currentEmployee())
    FILTERGROUP customer
        FILTER 'По проекту' project(i) == p 'F10'
;

DESIGN invoiceDashboard {
    NEW pane FIRST {
        fill = 1;
        type = SPLITV;
        NEW toInvoice {
            caption = 'Не выставлен счет';
            fill = 1;
            type = SPLITH;
            MOVE BOX(p);
            MOVE BOX(t) {
                fill = 2;
            }
        }
        MOVE BOX(i);
        
    }
}

NAVIGATOR {
    dashboard {
        NEW invoiceDashboard;
    }
}

// Выставление счетов
in 'Отм' = DATA LOCAL BOOLEAN (TimeEntry);
countTimeEntry = GROUP SUM 1 IF in(TimeEntry e);

createInvoice 'Выставить счет' (Project p)  { 
    NEWSESSION NESTED (in) {
        NEW i = Invoice {
            project(i) <- p;
            author(i) <- currentEmployee();
            invoice(TimeEntry e) <- i WHERE in(e);
            DIALOG invoice OBJECTS i = i DO {
                SEEK invoiceDashboard.i = i;
            }
        }
    }
}

EXTEND FORM invoiceDashboard
    PROPERTIES(t) in BEFORE namePractice(t)
    PROPERTIES(p) createInvoice TODRAW t SHOWIF countTimeEntry() TOOLBAR
;
