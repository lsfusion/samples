MODULE Employee;

REQUIRE MasterData, Authentication, Security, Utils;

CLASS Employee 'Сотрудник' : CustomUser;
TABLE employee (Employee);

code 'Код' = DATA VARSTRING[10] (Employee);

FORM employee 'Сотрудник'
    OBJECTS e = Employee PANEL
    PROPERTIES(e) firstName, lastName, code, login, sha256Password ON CHANGE changeSHA256Password(e), nameMainRole 

    EDIT Employee OBJECT e
;

FORM employees 'Сотрудники'
    OBJECTS e = Employee
    PROPERTIES(e) READONLYIF isReadonly() firstName, lastName, code, login, nameMainRole
    
    PROPERTIES(e) NEWSESSION NEW, EDIT, DELETE 
;
@extendFormEditable(employees);

FORM dialogEmployee 'Выбор сотрудника'
    OBJECTS e = Employee
    PROPERTIES(e) READONLY firstName, lastName, code
    
    LIST Employee OBJECT e
;

NAVIGATOR {
    masterData {
        NEW employees;
    }
}

currentEmployee = currentUser() AS Employee;

// ------------- Практика

CLASS Practice 'Практика';
TABLE practice (Practice);

name 'Наименование' = DATA VARSTRING[50] (Practice) IN recognize CHARWIDTH 15;

FORM practice 'Практика'
    OBJECTS p = Practice PANEL
    PROPERTIES(p) name
    
    EDIT Practice OBJECT p
;

FORM practices 'Практики'
    OBJECTS p = Practice
    PROPERTIES(p) READONLY name
    PROPERTIES(p) NEWSESSION NEW, EDIT, DELETE
;

FORM dialogPractices 'Выбор практики'
    OBJECTS p = Practice
    PROPERTIES(p) READONLY name
    
    LIST Practice OBJECT p
;

NAVIGATOR {
    masterData {
        NEW practices;
    }
}

practice 'Практика' = DATA Practice (Employee);
namePractice 'Практика' (Employee e) = name(practice(e));

EXTEND FORM employees
    PROPERTIES(e) READONLYIF isReadonly() namePractice
;

EXTEND FORM employee
    PROPERTIES(e) namePractice
;