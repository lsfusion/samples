MODULE UseCaseNewSession;

CLASS Order 'Order';

isPosted 'Completed' = DATA BOOLEAN (Order);
number 'Number' = DATA INTEGER (Order);

FORM orders
    OBJECTS o = Order
    PROPERTIES(o) READONLY isPosted, number
;

post 'Complete' (Order o)  { 
    NEWSESSION {
        isPosted(o) <- TRUE;
        APPLY;
    }
}

EXTEND FORM orders
    PROPERTIES(o) post TOOLBAR
;

FORM order
    OBJECTS o = Order PANEL
    PROPERTIES(o) isPosted, number
    
    EDIT Order OBJECT o
;

newOrder ()  { 
    NEWSESSION {
        NEW o = Order {
            number(o) <- (GROUP MAX number(Order oo)) (+) 1;
            SHOW order OBJECTS o = o;
        }
    }
}

EXTEND FORM orders
    // Option 1
    PROPERTIES() newOrder DRAW o TOOLBAR
    
    // Option 2
    PROPERTIES(o) NEWSESSION NEW
;

selected 'Mark' = DATA LOCAL BOOLEAN (Order);
EXTEND FORM orders
    PROPERTIES(o) selected
;

deleteSelectedOrders 'Delete marked orders' ()  { 
    NEWSESSION NESTED(selected) {
        DELETE Order o WHERE selected(o);
        ASK 'You are about to delete ' + (GROUP SUM 1 IF DROPPED (Order o)) + ' orders. Continue?' DO {
            APPLY;
        }
    }
}

EXTEND FORM orders
    PROPERTIES() deleteSelectedOrders DRAW o TOOLBAR
;

CLASS Payment 'Payment';

date 'Date' = DATA DATE (Payment);
sum 'Amount' = DATA NUMERIC[14,2] (Payment);

order 'Order' = DATA Order (Payment);

FORM orderPayments 'Order payments'
    OBJECTS o = Order PANEL // Not adding properties so that this object is not visible on the form at all
    
    OBJECTS p = Payment
    PROPERTIES(p) date, sum, NEW, DELETE
    FILTERS order(p) == o
;

editPayments 'Edit payments' (Order o)  { 
    NESTEDSESSION {
        SHOW orderPayments OBJECTS o = o;
    }
}

EXTEND FORM order
    PROPERTIES(o) editPayments
;

