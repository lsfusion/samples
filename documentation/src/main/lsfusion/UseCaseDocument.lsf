MODULE UseCaseDocument;

REQUIRE Utils;

//#sample1
CLASS Book 'Book';
name 'Name' = DATA ISTRING[100] (Book) IN id;
  
CLASS Order 'Order';
date 'Date' = DATA DATE (Order);
number 'Number' = DATA STRING[10] (Order);
  
CLASS OrderDetail 'Order line';
order 'Order' = DATA Order (OrderDetail) NONULL DELETE;
  
book 'Book' = DATA Book (OrderDetail) NONULL;
nameBook 'Book' (OrderDetail d) = name(book(d));
  
quantity 'Quantity' = DATA INTEGER (OrderDetail);
price 'Price' = DATA NUMERIC[14,2] (OrderDetail);
//#sample1 end

//#solution1
FORM order 'Order'
    OBJECTS o = Order PANEL
    PROPERTIES(o) date, number
  
    OBJECTS d = OrderDetail
    PROPERTIES(d) nameBook, quantity, price, NEW, DELETE
    FILTERS order(d) == o
  
    EDIT Order OBJECT o
;
 
  
FORM orders 'Orders'
    OBJECTS o = Order
    PROPERTIES(o) READONLY date, number
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
;
  
NAVIGATOR {
    NEW orders;
}
//#solution1 end

//#solution2
EXTEND FORM orders
    OBJECTS d = OrderDetail
    PROPERTIES(d) READONLY nameBook, quantity, price
    FILTERS order(d) == o
;
//#solution2 end

//#sample3
posted 'Completed' = DATA BOOLEAN (Order);
reserved 'Reserved' (Book b) = GROUP SUM quantity(OrderDetail d) IF posted(order(d)) BY book(d);

EXTEND FORM order
    PROPERTIES(o) posted
; 
//#sample3 end

//#solution3
post 'Complete' (Order o)  { posted(o) <- TRUE; }

EXTEND FORM order
    EVENTS ON OK post(o)
;
DESIGN order {
    PROPERTY(formOk()) {
        caption = 'Complete';
    }
}
//#solution3 end

//#solution4
quantity 'Qty' (Order o, Book b) = GROUP SUM quantity(OrderDetail d) BY order(d), book(d);
lastOrderDetail 'Last line' (Order o, Book b) = GROUP LAST OrderDetail d ORDER d BY order(d), book(d);

changeQuantity 'Change quantity' (Order o, Book b)  { 
    INPUT q = INTEGER DO { // requesting a number
        IF lastOrderDetail(o, b) THEN { // checking if there is at least one row
            IF q THEN // inputting a number
                quantity(OrderDetail d) <- q IF d == lastOrderDetail(o, b) WHERE order(d) == o AND book(d) == b; // writing the quantity in the last row with such a book
            ELSE // the number is dropped - deleting the row
                DELETE OrderDetail d WHERE order(d) == o AND book(d) == b;   
        } ELSE
            IF q THEN
                NEW d = OrderDetail { // creating a new row
                    order(d) <- o;
                    book(d) <- b;
                    quantity(d) <- q;
                }
    }
}  

EXTEND FORM order
    OBJECTS b = Book
    PROPERTIES name(b) READONLY, quantity(o, b) ON CHANGE changeQuantity(o, b)
;

DESIGN order {
    OBJECTS {
        NEW pane { // creating a container after the order header
            fill = 1;
            tabbed = TRUE;
            MOVE BOX(d);
            MOVE BOX(b) {
                caption = 'Selection';
            }
        }
    }
}

//#solution4 end

showCaseAction (INTEGER q, Order o, Book b)  { 
//#solution4a
quantity(OrderDetail d) <- q WHERE d == lastOrderDetail(o, b);
//#solution4a end
}

//#solution5a
CLASS ABSTRACT Invoice 'Invoice';
date 'Date' = ABSTRACT DATE (Invoice);
number 'Number' = ABSTRACT STRING[10] (Invoice);

CLASS ABSTRACT InvoiceDetail 'Invoice line';
invoice 'Invoice' = ABSTRACT Invoice (InvoiceDetail);
book 'Book' = ABSTRACT Book (InvoiceDetail);

quantity 'Quantity' = ABSTRACT INTEGER (InvoiceDetail);
price 'Price' = ABSTRACT NUMERIC[14,2] (InvoiceDetail);
//#solution5a end

//#solution5b
FORM invoices 'Invoices'
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY date, number
    PROPERTIES(i) NEWSESSION edit, DELETE
;

NAVIGATOR {
    NEW invoices;
}
//#solution5b end

//#solution5c
CLASS UserInvoice 'Custom invoice' : Invoice;
date 'Date' = DATA DATE (UserInvoice);
number 'Number' = DATA STRING[10] (UserInvoice);

CLASS UserInvoiceDetail 'Custom invoice line' : InvoiceDetail;
userInvoice 'Custom invoice' = DATA UserInvoice (UserInvoiceDetail); 
book 'Book' = DATA Book (UserInvoiceDetail);
nameBook 'Book' (UserInvoiceDetail d) = name(book(d));

quantity 'Quantity' = DATA INTEGER (UserInvoiceDetail);
price 'Price' = DATA NUMERIC[14,2] (UserInvoiceDetail);

// implementing invoice properties
date(UserInvoice i) += date(i);
number(UserInvoice i) += number(i);

// implementing the properties of the invoice lines
invoice (UserInvoiceDetail d) += userInvoice(d);
book (UserInvoiceDetail d) += book(d);

quantity (UserInvoiceDetail d) += quantity(d);
price (UserInvoiceDetail d) += price(d);
//#solution5c end

//#solution5d
FORM userInvoice 'Invoice (custom)'
    OBJECTS i = UserInvoice PANEL
    PROPERTIES(i) date, number

    OBJECTS d = UserInvoiceDetail
    PROPERTIES(d) nameBook, quantity, price, NEW, DELETE
    FILTERS userInvoice(d) == i
    
    EDIT UserInvoice OBJECT i
;

EXTEND FORM invoices
    PROPERTIES(i) NEWSESSION NEW[UserInvoice]
;
//#solution5d end

//#solution5e
createInvoice 'Create invoice' = DATA BOOLEAN (Order);
EXTEND FORM order PROPERTIES(o) createInvoice;

// aggregated invoice
CLASS OrderInvoice 'Invoice based on order' : Invoice; 
orderInvoice = AGGR OrderInvoice WHERE createInvoice(Order order) MATERIALIZED INDEXED;
date(OrderInvoice i) += date(order(i));
number(OrderInvoice i) += number(order(i));

// aggregated invoice lines
createInvoiceDetail 'Create invoice line' (OrderDetail d) = createInvoice(order(d));

CLASS OrderInvoiceDetail 'Invoice line based on order' : InvoiceDetail;
orderInvoiceDetail = AGGR OrderInvoiceDetail WHERE createInvoiceDetail(OrderDetail orderDetail) MATERIALIZED INDEXED;

invoice (OrderInvoiceDetail d) += orderInvoice(order(orderDetail(d)));
book (OrderInvoiceDetail d) += book(orderDetail(d));  

quantity (OrderInvoiceDetail d) += quantity(orderDetail(d));  
price (OrderInvoiceDetail d) += price(orderDetail(d));
//#solution5e end

//#solution5f
edit(OrderInvoice i) + {  edit(order(i)); }
//#solution5f end