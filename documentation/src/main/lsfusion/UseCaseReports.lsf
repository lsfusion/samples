MODULE UseCaseReports;

//#sample1
NAMESPACE Sample;

CLASS Category 'Category';
name 'Name' = DATA ISTRING[50] (Category) IN id;

CLASS Book 'Book';
name 'Name' = DATA ISTRING[100] (Book) IN id;

category 'Category' = DATA Category (Book) AUTOSET;
nameCategory 'Category' (Book b) = name(category(b)) IN id;

countBooks 'Number of books' (Category c) = GROUP SUM 1 BY category(Book b);

FORM books 'Books'
    OBJECTS b = Book
    PROPERTIES(b) READONLY name, nameCategory
    PROPERTIES(b) NEWSESSION NEW, EDIT, DELETE
; 

NAVIGATOR {
    NEW books;
}
//#sample1 end

//#solution1a
FORM booksByCategories 'Books by category'
    OBJECTS c = Category
    PROPERTIES(c) name, countBooks
    
    OBJECTS b = Book
    PROPERTIES(b) name, nameCategory
    FILTERS category(b) == c 
;
//#solution1a end

//#solution1b
printBooksByCategories 'Books by category' ()  { 
    PRINT booksByCategories;
}

xlsxBooksByCategories 'Books by category (XLSX)' ()  { 
    PRINT booksByCategories XLSX;
}
//#solution1b end

//#solution1c
EXTEND FORM books
    PROPERTIES() DRAW b TOOLBAR printBooksByCategories, xlsxBooksByCategories
;
//#solution1c end

//#sample2
CLASS Invoice 'Invoice';
date 'Date' = DATA DATE (Invoice);
number 'Number' = DATA STRING[10] (Invoice);
  
CLASS InvoiceDetail 'Invoice line';
invoice 'Invoice' = DATA Invoice (InvoiceDetail) NONULL DELETE;
  
book 'Book' = DATA Book (InvoiceDetail) NONULL;
nameBook 'Book' (InvoiceDetail d) = name(book(d));
  
quantity 'Quantity' = DATA INTEGER (InvoiceDetail);
price 'Price' = DATA NUMERIC[14,2] (InvoiceDetail);

FORM invoice 'Invoice'
    OBJECTS i = Invoice PANEL
    PROPERTIES(i) date, number
    
    OBJECTS d = InvoiceDetail
    PROPERTIES(d) nameBook, quantity, price, NEW, DELETE
    FILTERS invoice(d) == i
    
    EDIT Invoice OBJECT i
;

FORM invoices 'Invoices'
    OBJECTS i = Invoice
    PROPERTIES(i) READONLY date, number
    PROPERTIES(i) NEWSESSION NEW, EDIT, DELETE
;

NAVIGATOR {
    NEW invoices;
}
//#sample2 end

//#solution2
print 'Print' (Invoice i)  { 
    PRINT invoice OBJECTS i = i;
}
printWord 'Print (DOCX)' (Invoice i)  { 
    PRINT invoice OBJECTS i = i DOCX;
}

EXTEND FORM invoices
    PROPERTIES(i) print TOOLBAR, printWord TOOLBAR
;
//#solution2 end

//#sample3
CLASS Customer 'Customer';
name 'Name' = DATA ISTRING[50] (Customer) IN id;

FORM customers 'Customers'
    OBJECTS c = Customer
    PROPERTIES(c) READONLY name
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE
;

NAVIGATOR {
    NEW customers;
}

customer 'Customer' = DATA Customer (Invoice);
nameCustomer 'Customer' (Invoice i) = name(customer(i));

EXTEND FORM invoice PROPERTIES(i) nameCustomer;

CLASS Order 'Order';
date 'Date' = DATA DATE (Order);
number 'Number' = DATA STRING[10] (Order);

customer 'Customer' = DATA Customer (Order);
nameCustomer 'Customer' (Order o) = name(customer(o));

CLASS OrderDetail 'Order line';
order 'Order' = DATA Order (OrderDetail) NONULL DELETE;
  
book 'Book' = DATA Book (OrderDetail) NONULL;
nameBook 'Book' (OrderDetail d) = name(book(d));
  
quantity 'Quantity' = DATA INTEGER (OrderDetail);
price 'Price' = DATA NUMERIC[14,2] (OrderDetail);

FORM order 'Order'
    OBJECTS o = Order PANEL
    PROPERTIES(o) date, number, nameCustomer
    
    OBJECTS d = OrderDetail
    PROPERTIES(d) nameBook, quantity, price, NEW, DELETE
    FILTERS order(d) == o
    
    EDIT Order OBJECT o
;

FORM orders 'Orders'
    OBJECTS i = Order
    PROPERTIES(i) READONLY date, number, nameCustomer
    PROPERTIES(i) NEWSESSION NEW, EDIT, DELETE
;

NAVIGATOR {
    NEW orders;
}
//#sample3 end

//#solution3
FORM customerInfo 'Customer information'
    OBJECTS c = Customer PANEL
    PROPERTIES(c) name
    
    OBJECTS o = Order
    PROPERTIES(o) date, number
    FILTERS customer(o) == c
    
    OBJECTS od = OrderDetail
    PROPERTIES(od) nameBook, quantity, price
    FILTERS order(od) == o
    
    OBJECTS i = Invoice
    PROPERTIES(i) date, number
    FILTERS customer(i) == c
    
    OBJECTS id = InvoiceDetail
    PROPERTIES(id) nameBook, quantity, price
    FILTERS invoice(id) == i
;

printInfo 'Print information' (Customer c)  { 
    PRINT customerInfo OBJECTS c = c;
}

EXTEND FORM customers
    PROPERTIES(c) printInfo TOOLBAR
;
//#solution3 end