MODULE CRUDUseCase;

CLASS Type 'Type' {
    novel 'Novel',
    thriller 'Thriller',
    fiction 'Fiction'
}
name 'Name' (Type g) = staticCaption(g) IF g IS Type;

FORM types 'List of types'
    OBJECTS g = Type
    PROPERTIES(g) READONLY name
 
    LIST Type OBJECT g
;

CLASS Book 'Book';
name 'Name' = DATA ISTRING[30] (Book) IN id;

FORM book 'Book' // form for displaying "card' // form for displaying the book card
    OBJECTS b = Book PANEL
    PROPERTIES(b) name
     
    EDIT Book OBJECT b
;
  
FORM books 'Books'
    OBJECTS b = Book
    PROPERTIES(b) READONLY name
    PROPERTIES(b) NEWSESSION NEW, EDIT, DELETE
  
    LIST Book OBJECT b
;
  
NAVIGATOR {
    NEW books;
}

CLASS Genre 'Genre';
name 'Name' = DATA ISTRING[30] (Genre);

FORM genre 'Genre'
    OBJECTS g = Genre PANEL
    PROPERTIES(g) name
     
    EDIT Genre OBJECT g
;
  
FORM genres 'Genres'
    OBJECTS g = Genre
    PROPERTIES(g) READONLY name
    PROPERTIES(g) NEWSESSION NEW, EDIT, DELETE
;
 
FORM dialogGenre 'Genres'
    OBJECTS g = Genre
    PROPERTIES(g) READONLY name
  
    LIST Genre OBJECT g
;
  
NAVIGATOR {
    NEW genres;
}
