MODULE UseCaseSum;

CLASS Book 'Book';
CLASS Category 'Category';
  
category 'Category' = DATA Category (Book);

countBooks 'Number of books' (Category c) = GROUP SUM 1 BY category(Book book);

CLASS Tag 'Tag';
  
in 'On' = DATA BOOLEAN (Tag, Book);

countBooks 'Number of books' (Tag t) = GROUP SUM 1 IF in(t, Book b);

CLASS Stock 'Warehouse';
 
  
CLASS Ledger 'Movement';
book 'Book' = DATA Book (Ledger);
stock 'Warehouse' = DATA Stock (Ledger);
  
quantity 'Qty' = DATA INTEGER (Ledger);
out 'Expenses' = DATA BOOLEAN (Ledger);

TABLE bookStock (Book, Stock);
currentBalance 'Current balance' (Book b, Stock s) = GROUP SUM IF out(Ledger l) THEN -quantity(l) ELSE quantity(l) BY book(l), stock(l) MATERIALIZED;

date 'Date' = DATA DATE (Ledger) INDEXED; // it is better to add an index to filter by date quickly

// Option 1
balance1 'Balance as of date' (Book b, Stock s, DATE d) = GROUP SUM (IF out(Ledger l) THEN -quantity(l) ELSE quantity(l)) IF date(l) < d BY book(l), stock(l);
  
// Option 2
balance2 'Balance as of date' (Book b, Stock s, DATE d) = currentBalance(b, s) (-) [ GROUP SUM (IF out(Ledger l) THEN -quantity(l) ELSE quantity(l)) IF date(l) >= d BY book(l), stock(l)](b, s);

currentBalance 'Current balance' (Book b) = GROUP SUM currentBalance(b, Stock s);
