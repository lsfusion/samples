MODULE UseCasePartition;

CLASS Order 'Order';
CLASS OrderDetail 'Order line';
  
order 'Order' = DATA Order (OrderDetail) NONULL DELETE;

index 'Line number' (OrderDetail d) = PARTITION SUM 1 ORDER d BY order(d) CHARWIDTH 4;

date 'Date' = DATA DATE (Order);
  
CLASS Customer 'Customer';
customer 'Customer' = DATA Customer (Order);

prevOrderDate 'Previous order' (Order o) = PARTITION PREV date(o) ORDER date(o), o BY customer(o);

CLASS Book 'Book';
  
CLASS Batch 'Batch';
book 'Book' = DATA Book (Batch);
date 'Arrival date' = DATA DATE (Batch);
  
CLASS Stock 'Warehouse';
currentBalance 'Balance' = DATA INTEGER (Batch, Stock); // The balance is made data for the example. This is usually a calculated property.

quantity = DATA LOCAL INTEGER (Book);
  
quantityFIFO 'Quantity by FIFO batch' (Batch b, Stock s) = PARTITION UNGROUP quantity
                                                                    LIMIT STRICT currentBalance(b, s)
                                                                    ORDER date(b), b
                                                                    BY book(b);
