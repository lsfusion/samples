MODULE UseCaseColumns;

REQUIRE Utils;

CLASS Book 'Book';
name 'Name' = DATA ISTRING[50] (Book) IN id;

CLASS Customer 'Customer';
name 'Name' = DATA ISTRING[50] (Customer) IN id;

price 'Price' = DATA NUMERIC[14,2] (Book, Customer);

FORM prices 'Prices'
    OBJECTS c = Customer
    
    OBJECTS b = Book
    PROPERTIES name(b) READONLY, price(b, c) COLUMNS (c) HEADER name(c)
;

gracePeriod 'Delay, days' = DATA INTEGER (Book, Customer); 

selected 'Mark' = DATA BOOLEAN (Customer);

headerName 'Price header' (Customer c) = name(c) + ': Price';
headerGrace 'Dealy header' (Customer c) = name(c) + ': Delay, days';

FORM pricesAndGracePeriods 'Prices and delays'
    OBJECTS s = Customer
    PROPERTIES selected(s), name(s) READONLY
    
    OBJECTS c = Customer
    FILTERS selected(c)
    
    OBJECTS b = Book
    PROPERTIES name(b) READONLY, 
               price(b, c) COLUMNS 'priceAndGrace' (c) HEADER headerName(c), 
               gracePeriod(b, c) COLUMNS 'priceAndGrace' (c) HEADER headerGrace(c)
;

DESIGN pricesAndGracePeriods {
    BOX(b) {
        fill = 3;
        PROPERTY(gracePeriod(b, c)) {
            background = #FFFFAA;
        }
    }
}

in 'On' = DATA BOOLEAN (INTEGER, INTEGER);

row 'Row header' (INTEGER r) = 'Row ' + (r AS INTEGER);
column 'Column header' (INTEGER c) = 'Column ' + (c AS INTEGER);

FORM matrix 'Matrix'
    OBJECTS m = (rows = INTEGER, columns = INTEGER) PANEL
    PROPERTIES rs = VALUE(rows), cs = VALUE(columns)
    
    OBJECTS c = INTEGER
    FILTERS iterate(c, 1, columns)
    
    OBJECTS r = INTEGER
    PROPERTIES(r) row
    PROPERTIES in(r, c) COLUMNS (c) HEADER column(c)
    FILTERS iterate(r, 1, rows)
;

DESIGN matrix {
    PROPERTY(rs) { caption = 'Number of rows'; }
    PROPERTY(cs) { caption = 'Number of columns'; }
}
