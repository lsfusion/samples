MODULE UseCaseSeek;

REQUIRE Time;

CLASS Category 'Category';
name 'Name' = DATA ISTRING[50] (Category) IN id;

CLASS Book 'Book';
name 'Name' = DATA ISTRING[50] (Book) IN id;

category 'Category' = DATA Category (Book) NONULL;
nameCategory 'Category' (Book b) = name(category(b));

FORM book 'Book'
    OBJECTS b = Book PANEL
    PROPERTIES(b) name, nameCategory
    
    EDIT Book OBJECT b
;

FORM books 'Books'
    OBJECTS c = Category
    PROPERTIES(c) READONLY name
    PROPERTIES(c) NEWSESSION NEW, EDIT, DELETE
    
    OBJECTS b = Book
    PROPERTIES(b) READONLY name
    FILTERS category(b) == c
;

NAVIGATOR {
    NEW books;
}

createBook 'Create book' (Category c)  { 
    NEWSESSION {
        NEW newBook = Book {
            category(newBook) <- c;
            DIALOG book OBJECTS b = newBook INPUT DO {
                SEEK books.b = newBook;
            }
        }
    }
}

EXTEND FORM books
    PROPERTIES(c) createBook DRAW b TOOLBAR
;

CLASS Customer 'Customer';
name 'Name' = DATA ISTRING[50] (Customer) IN id;

price 'Price' = DATA NUMERIC[14,2] (Customer, Book);

FORM prices 'Prices'
    OBJECTS c = Customer PANEL
    PROPERTIES(c) name SELECTOR
    
    OBJECTS b = Book
    PROPERTIES name(b) READONLY, price(c, b)
;

NAVIGATOR {
    NEW prices;
}

defaultCustomer 'Default customer' = DATA Customer ();
nameDefaultCustomer 'Default customer' () = name(defaultCustomer());

EXTEND FORM options PROPERTIES() nameDefaultCustomer;
DESIGN options { commons { MOVE PROPERTY(nameDefaultCustomer()); } }

EXTEND FORM prices
    EVENTS ON INIT { SEEK prices.c = defaultCustomer(); }
;

FORM report 'Report'
    OBJECTS dFrom = DATE PANEL, dTo = DATE PANEL
    PROPERTIES VALUE(dFrom), VALUE(dTo)
;

NAVIGATOR {
    NEW report;
}

setReportLastWeek 'Last week' ()  { 
    SEEK report.dFrom = subtract(currentDate(), 7);
    SEEK report.dTo = subtract(currentDate(), 1); 
}
setReportCurrentMonth 'Current month' ()  { 
    SEEK report.dFrom = firstDayOfMonth(currentDate());
    SEEK report.dTo = lastDayOfMonth(currentDate());
}
setReportLastMonth 'Last month' ()  { 
    SEEK report.dFrom = firstDayOfMonth(subtract(firstDayOfMonth(currentDate()), 1));
    SEEK report.dTo = subtract(firstDayOfMonth(currentDate()), 1);
}

EXTEND FORM report
    PROPERTIES() setReportLastWeek, setReportCurrentMonth, setReportLastMonth
;

FORM customers 'Customers'
    OBJECTS c = Customer
    PROPERTIES(c) name, NEW, DELETE
;

NAVIGATOR {
    NEW customers;
}