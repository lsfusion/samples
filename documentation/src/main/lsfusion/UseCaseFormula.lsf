MODULE UseCaseFormula;

CLASS Order 'Order';
date 'Date' = DATA DATE (Order);
number 'Number' = DATA STRING[30] (Order);
  
FORM orders 'Purchase orders'
    OBJECTS o = Order
    PROPERTIES(o) date, number, NEW, DELETE
;

NAVIGATOR {
    NEW orders;
}

toISO = FORMULA STRING[10] 'to_char($1,\'YYYY-MM-DD\')';

exportToCSV 'Export to CSV' () {
    LOCAL file = FILE ();
    EXPORT CSV FROM toISO(date(Order o)), number(o) TO file;
    open(file()); 
}

EXTEND FORM orders
    PROPERTIES() exportToCSV
;

CLASS OrderDetail;
order = DATA Order (OrderDetail) NONULL DELETE;

quantity 'Qty' = DATA NUMERIC[14,3] (OrderDetail);
sum 'Amount' = DATA NUMERIC[14,2] (OrderDetail);

EXTEND FORM orders
    OBJECTS d = OrderDetail
    PROPERTIES(d) quantity, sum, NEW, DELETE
    FILTERS order(d) = o
;

toString = FORMULA TEXT 'to_char($1,$2)';

exportToCSV 'Export to CSV' (Order o) {
    LOCAL file = FILE ();
    EXPORT CSV FROM toISO(date(o)), number(o), toString(quantity(OrderDetail d), '999 999.999'), toString(sum(d), '999 999.99') WHERE order(d) = o TO file;
    open(file()); 
}

EXTEND FORM orders
    PROPERTIES(o) exportToCSV
;

onlyDigits = FORMULA NULL BOOLEAN 'CASE WHEN trim($1) ~ \'^[0-9]*$\' THEN 1 ELSE NULL END';

EXTEND FORM orders
    PROPERTIES 'Only numbers' = onlyDigits(number(o))
;
