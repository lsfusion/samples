MODULE UseCaseEvents;

REQUIRE Time, Authentication;

CLASS Order 'Order';
isClosed 'Closed' = DATA BOOLEAN (Order);
date 'Date' = DATA DATE (Order);
number 'Number' = DATA INTEGER (Order);

// Option 1
WHEN SET(date(Order o) < currentDate()) DO
    isClosed(o) <- TRUE;

// Option 2
WHEN CHANGED(currentDate()) AND date(Order o) < currentDate() DO
    isClosed(o) <- TRUE;

CLASS OrderDetail 'Order line';
order 'Order' = DATA Order (OrderDetail) NONULL DELETE;

quantity 'Qty' = DATA NUMERIC[15,3] (OrderDetail);
price 'Price' = DATA NUMERIC[14,2] (OrderDetail);
sum 'Amount' = DATA NUMERIC[16,2] (OrderDetail);

WHEN LOCAL (CHANGED(quantity(OrderDetail d)) OR CHANGED(price(d)))
            AND NOT CHANGED(sum(d)) DO {
    sum(d) <- NUMERIC[16,2](quantity(d) * price(d));            
}

CLASS Book 'Book';
name 'Name' = DATA ISTRING[50] (Book);
price 'Price' = DATA NUMERIC[14,2] (Book);

book 'Book' = DATA Book (OrderDetail);
nameBook 'Book' (OrderDetail d) = name(book(d));

FORM order
    OBJECTS o = Order PANEL
    PROPERTIES(o) date, number
    
    OBJECTS d = OrderDetail
    PROPERTIES(d) nameBook, quantity, price, sum
    FILTERS order(d) == o
;

WHEN LOCAL FORMS order SETCHANGED(book(OrderDetail d)) DO {
    price(d) <- price(book(d));
}  

CLASS OrderLog 'Order deletion log';
date 'Order date' = DATA DATE (OrderLog);
number 'Order number' = DATA INTEGER (OrderLog);

dateTime 'Date' = DATA DATETIME (OrderLog);
user 'User' = DATA User (OrderLog);

WHEN DROPPED(Order o IS Order) DO {
    NEW l = OrderLog {
        date(l) <- PREV(date(o));
        number(l) <- PREV(number(o));
        
        dateTime(l) <- currentDateTime();
        user(l) <- currentUser();
    }
}
