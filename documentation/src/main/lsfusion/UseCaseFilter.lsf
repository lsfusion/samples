MODULE UseCaseFilter;

//#sample1
CLASS Book 'Book';
name 'Name' = DATA ISTRING[100] (Book);
  
CLASS Stock 'Warehouse';
name 'Name' = DATA ISTRING[100] (Stock);
  
balance 'Balance' = DATA INTEGER (Book, Stock); // for example it is made a data property, although usually it is calculated
//#sample1 end

//#solution1
FORM onStockObject 'Balances'
    OBJECTS s = Stock PANEL
    PROPERTIES(s) name SELECTOR
  
    OBJECTS b = Book
    PROPERTIES READONLY name(b), balance(b, s)
    ORDERS name(b)
  
    // Option 1
    FILTERS balance(b, s)
  
    // Option 2
    FILTERGROUP bal
        FILTER 'With positive balance' balance(b, s) > 0 'F10'
        FILTER 'With negative balance' balance(b, s) < 0 'F9'
        FILTER 'With balance' balance(b, s) 'F8' DEFAULT
        FILTER 'No remainder' NOT balance (b, s) 'F7'
;
//#solution1 end

//#solution2
filterStock = DATA LOCAL Stock ();
nameFilterStock 'Warehouse' = name(filterStock());
  
FORM onStockLocal 'Balances'
    PROPERTIES() nameFilterStock
  
    OBJECTS sb = (s = Stock, b = Book)
    PROPERTIES READONLY name(s), name(b), balance(b, s)
    ORDERS name(s), name(b)
  
    FILTERS s == filterStock() OR NOT filterStock()
;
//#solution2 end

//#sample3
CLASS Customer 'Customer';
name 'Name' = DATA ISTRING[100] (Customer);
  
CLASS Order 'Order';
date 'Date' = DATA DATE (Order);
  
customer 'Customer' = DATA Customer (Order);
nameCustomer 'Customer' (Order o) = name(customer(o));
//#sample3 end

//#solution3
filterCustomer = DATA LOCAL Customer ();
nameFilterCustomer 'Customer' = name(filterCustomer());
  
FORM orders 'Orders'
    PROPERTIES() nameFilterCustomer
  
    OBJECTS dates = (dateFrom = DATE, dateTo = DATE) PANEL
    PROPERTIES df = VALUE(dateFrom), dt = VALUE(dateTo)
  
    OBJECTS o = Order
    PROPERTIES(o) READONLY do = date, nameCustomer
    ORDERS do DESC
    FILTERS date(o) >= dateFrom, date(o) <= dateTo,
            customer(o) == filterCustomer() OR NOT filterCustomer()
;
//#solution3 end
