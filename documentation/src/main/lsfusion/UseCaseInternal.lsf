MODULE UseCaseInternal;

ip = DATA LOCAL TEXT();
getIPJava INTERNAL 'GetIP';
showIPJava 'Show computer name (Java)' {
    getIPJava();
    MESSAGE ip();
}

FORM info 'Information'
    PROPERTIES() showIPJava
;

NAVIGATOR {
    NEW info;
}

getIPFusion INTERNAL <{ findProperty("ip").change((Object)java.net.InetAddress.getLocalHost().toString(), context); }>;
showIPFusion 'Show computer name (Fusion)' {
    getIPFusion();
    MESSAGE ip();
}

EXTEND FORM info
    PROPERTIES() showIPFusion
;

gcd = DATA LOCAL INTEGER();
calculateGCD 'Calculate GCD' INTERNAL 'CalculateGCD' (INTEGER, INTEGER);

FORM gcd 'GCD'
    OBJECTS (a = INTEGER, b = INTEGER) PANEL
    PROPERTIES 'A' = VALUE(a), 'B' = VALUE(b)
    
    PROPERTIES gcd(), calculateGCD(a, b)
;

NAVIGATOR {
    NEW gcd;
}

CLASS Calculation;
a = DATA INTEGER (Calculation);
b = DATA INTEGER (Calculation);
gcd = DATA INTEGER (Calculation);
calculateGCD 'Calculate GCD' INTERNAL 'CalculateGCDObject' (Calculation);

EXTEND FORM gcd
    OBJECTS c = Calculation
    PROPERTIES(c) a, b, gcd, calculateGCD GRID, NEW, DELETE 
;

beep INTERNAL 'Beep';
FORM beep 'Signal'
    PROPERTIES() beep
;

NAVIGATOR {
    NEW beep;
}
