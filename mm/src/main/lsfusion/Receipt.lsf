//#header
//#module
MODULE Receipt;
//#module end

//#require
REQUIRE Stock, Item, LegalEntity;
//#require end
//#header end

//#class
CLASS Receipt 'Приходная накладная';
CLASS ReceiptDetail 'Строка приходной накладной';
//#class end

//#documentref
receipt 'Документ строки' = DATA Receipt (ReceiptDetail) NONULL DELETE;
//#documentref end

//#index
index 'Номер строки' (ReceiptDetail d) =
        PARTITION SUM 1 IF d IS ReceiptDetail
        ORDER d BY receipt(d);
//#index end

//#headerattributes
number 'Номер накладной' = DATA BPSTRING[10] (Receipt);
date 'Дата накладной' = DATA DATE (Receipt);

supplier 'Поставщик' = DATA LegalEntity (Receipt);
nameSupplier 'Наименование поставщика' (Receipt r) = name(supplier(r));

stock 'Склад' = DATA Stock(Receipt);
nameStock 'Наименование склада' (Receipt r) = name(stock(r));
//#headerattributes end

//#detailattributes
item 'Товар' (receiptDetail) = DATA Item (ReceiptDetail);
nameItem 'Наименование товара' (ReceiptDetail d) = name(item(d));

quantity 'Количество' = DATA NUMERIC[16,4] (ReceiptDetail);
price 'Цена поставщика' = DATA NUMERIC[17,2] (ReceiptDetail);
sum 'Сумма поставщика' (ReceiptDetail d) = quantity(d) * price(d);
//#detailattributes end

//#formedit
FORM receipt 'Приходная накладная'
	OBJECTS r=Receipt PANEL
	PROPERTIES(r) number, date, nameSupplier, nameStock

	OBJECTS d=ReceiptDetail
	PROPERTIES(d) index, nameItem, quantity, price, sum READONLY, NEW, DELETE GRID
	FILTERS receipt(d) = r

	EDIT Receipt OBJECT r
;
//#formedit end

//#formlist
FORM receipts 'Приходные накладные'
	OBJECTS r=Receipt
	PROPERTIES(r) READONLY number, date, nameSupplier, nameStock
	PROPERTIES(r) NEWSESSION NEW, EDIT, DELETE 

	OBJECTS d=ReceiptDetail
	PROPERTIES(d) READONLY index, nameItem, quantity, price, sum
	FILTERS receipt(d) = r
;
//#formlist end