//#module
MODULE Shipment;
//#module end

//#require
REQUIRE Stock, Item, LegalEntity;
//#require end

//#class
CLASS Shipment 'Расходная накладная';
CLASS ShipmentDetail 'Строка расходной накладной';

shipment 'Документ строки' = DATA Shipment (ShipmentDetail) NONULL DELETE;
index 'Номер строки' (ShipmentDetail d) =
        PARTITION SUM 1 IF d IS ShipmentDetail
        ORDER d BY shipment(d);
//#class end

//#headerattributes
number 'Номер накладной' = DATA BPSTRING[10] (Shipment);
date 'Дата накладной' = DATA DATE (Shipment);

customer 'Покупатель' = DATA LegalEntity (Shipment);
nameCustomer 'Наименование покупателя' (Shipment s) = name(customer(s));

stock 'Склад' = DATA Stock(Shipment);
nameStock 'Наименование склада' (Shipment s) = name(stock(s));
//#headerattributes end

//#detailattributes
item 'Товар' (shipmentDetail) = DATA Item (ShipmentDetail);
nameItem 'Наименование товара' (ShipmentDetail d) = name(item(d));

quantity 'Количество' = DATA NUMERIC[16,4](ShipmentDetail);
price 'Цена продажи' = DATA NUMERIC[17,2](ShipmentDetail);
sum 'Сумма продажи' (ShipmentDetail d) = quantity(d) * price(d);
//#detailattributes end

//#priceset
price(ShipmentDetail d) <- salePrice(item(d)) WHEN CHANGED(item(d));
//#priceset end

//#formedit
FORM shipment 'Расходная накладная'
	OBJECTS s=Shipment PANEL
	PROPERTIES(s) number, date, nameCustomer, nameStock

	OBJECTS d=ShipmentDetail
	PROPERTIES(d) nameItem, quantity, price, sum READONLY, NEW, DELETE GRID
	FILTERS shipment(d) == s

	EDIT Shipment OBJECT s
;
//#formedit end

//#formlist
FORM shipments 'Расходные накладные'
	OBJECTS s=Shipment
	PROPERTIES(s) READONLY number, date, nameCustomer, nameStock
	PROPERTIES(s) NEWSESSION NEW, EDIT, DELETE 

	OBJECTS d=ShipmentDetail
	PROPERTIES(d) READONLY nameItem, quantity, price, sum
	FILTERS shipment(d) == s
;
//#formlist end