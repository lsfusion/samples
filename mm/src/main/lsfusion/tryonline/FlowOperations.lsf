//#header
MODULE FlowOperations;

REQUIRE Shipment, StockItem;
//#header end
run() {
    LOCAL shippedTo = BOOLEAN (Item, LegalEntity); // tagging marked products/customers
    FOR currentBalance(Item i, Stock s) > 10 ORDER DESC name(i) DO { // for products/warehouses with a balance > 10 in the order of product names
        FOR stock(shipment(ShipmentDetail sd)) = s AND item(sd) = i DO // for all shipment lines with this item in the warehouse
            shippedTo(i, LegalEntity l) <- TRUE WHERE l = customer(shipment(sd)); // will be compiled to shippedTo(i, LegalEntity l) <- (GROUP MAX TRUE IF stock(shipment(ShipmentDetail sd)) = s AND item(sd) = i AND l=customer(shipment(sd)));
            
        IF salePrice(i) > 6 THEN
            BREAK; 
    }
    
    IF (GROUP SUM 1 IF shippedTo(Item i, LegalEntity l)) >= 2 THEN // number of positions >= 2
        exportString() <- GROUP CONCAT name(Item i) + ' -> ' + name(LegalEntity l) IF shippedTo(i, l),',\n' ORDER i; // returning product/customer names separated by commas with newlines 
	ELSE
    	exportString() <- 'Too few positions'; 
}