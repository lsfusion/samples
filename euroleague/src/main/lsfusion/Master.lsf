//#header
//#module
MODULE Master;
//#module end
//#header end

REQUIRE Utils, Time, Icon;

META defineMasterObject(object, caption, captions, nameLength)
    CLASS ###object caption;
    
    id 'Code' = DATA INTEGER (###object);
    object (INTEGER id) = GROUP AGGR ###object o BY id(o);
    
    name 'Name' = DATA ISTRING (###object) CHARWIDTH nameLength;
    object (ISTRING name) = GROUP AGGR ###object o BY name(o);
    
    FORM object caption
        OBJECTS o = ###object PANEL
        PROPERTIES(o) id, name
        
        EDIT ###object OBJECT o
    ;
    
    FORM object##s captions
        OBJECTS o = ###object
        PROPERTIES(o) READONLY id, name
        PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE
    ;
    
    FORM dialog###object##s captions
        OBJECTS o = ###object
        PROPERTIES(o) READONLY id, name
        
        LIST ###object OBJECT o
    ;
    
    NAVIGATOR {
        NEW object##s;
    }
END

@defineMasterObject(tournament, 'Tournament', 'Tournaments', 20);
@defineMasterObject(season, 'Season', 'Seasons', 5);
@defineMasterObject(round, 'Round', 'Rounds', 15);
@defineMasterObject(country, 'Country', 'Countries', 10);
@defineMasterObject(team, 'Club', 'Clubs', 20);

country = DATA Country (Team);
nameCountry 'Country' (Team t) = name(country(t));

EXTEND FORM team PROPERTIES(o) nameCountry;
EXTEND FORM teams PROPERTIES(o) READONLY nameCountry;