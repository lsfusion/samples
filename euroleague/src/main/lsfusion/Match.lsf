//#header
//#module
MODULE Match;
//#module end

REQUIRE League;
//#header end

CLASS Match 'Match';

id 'Code' = DATA INTEGER (Match);
match (INTEGER id) = GROUP AGGR Match o BY id(o);

dateTime 'Time' = DATA DATETIME (Match);  

league = DATA League (Match);

tournament (Match m) = tournament(league(m));
nameTournament 'Tournament' (Match m) = name(tournament(m));  

season(Match m) = season(league(m));
nameSeason 'Season' (Match m) = name(season(m));  

round = DATA Round (Match);
nameRound 'Round' (Match m) = name(round(m));

homeTeam = DATA Team (Match);
nameHomeTeam 'Hosts' (Match m) = name(homeTeam(m));

awayTeam = DATA Team (Match);
nameAwayTeam 'Guests' (Match m) = name(awayTeam(m));

goalsHome 'Goals (hosts)' = DATA INTEGER (Match);
goalsAway 'Goals (guests)' = DATA INTEGER (Match);

FORM match 'Match'
    OBJECTS o = Match PANEL
    PROPERTIES(o) id, dateTime, nameTournament, nameSeason, nameRound, 
                  nameHomeTeam, goalsHome, goalsAway, nameAwayTeam
    
    EDIT Match OBJECT o
;

FORM matches 'Matches'
    OBJECTS o = Match
    PROPERTIES(o) READONLY id, dateTime, nameTournament, nameSeason, nameRound, 
                           nameHomeTeam, goalsHome, goalsAway, nameAwayTeam
    PROPERTIES(o) NEWSESSION NEW, EDIT, DELETE

    LIST Match OBJECT o
;

NAVIGATOR {
    NEW matches;
}