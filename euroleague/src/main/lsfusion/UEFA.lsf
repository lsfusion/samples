//#header
//#module
MODULE Main;
//#module end

//#require
REQUIRE Match, Utils;
//#require end
//#header end

// Teams

isCL (Tournament t) = name(t) = 'Champions League';
isEL (Tournament t) = name(t) = 'Europa League';

isUL (Tournament t) = isCL(t) OR isEL(t);
isUL (Match m) = isUL(tournament(m));  

played (Team t, Match m) = homeTeam(m) = t OR awayTeam(m) = t;
won (Team t, Match m) = (homeTeam(m) = t AND goalsHome(m) > goalsAway(m)) OR (awayTeam(m) = t AND goalsHome(m) < goalsAway(m));
draw (Team t, Match m) = played(t, m) AND goalsHome(m) = goalsAway(m);

dataMatchCoeff = DATA NUMERIC[10,1] (Round);
matchCoeff 'Match odds' (Round r) = OVERRIDE dataMatchCoeff(r), 1.0;

wonPoints 'Points for victories' (Season s, Team t) = 
    GROUP SUM 2 * matchCoeff(round(Match m)) IF won(t, m) AND season(m) = s AND isUL(m);
drawPoints 'Points for a draw' (Season s, Team t) = 
    GROUP SUM 1 * matchCoeff(round(Match m)) IF draw(t, m) AND season(m) = s AND isUL(m);
matchPoints 'Points for matches' (Season s, Team t) = wonPoints(s, t) (+) drawPoints(s, t) MATERIALIZED;

played 'Played' (Season s, Tournament t, Round r, Team tm) = 
    GROUP SUM 1 IF played(tm, Match m) AND round(m) = r AND tournament(m) = t AND season(m) = s;
    
bonusPoints 'Pass bonus' = DATA NUMERIC[10,1] (Tournament, Round);
bonusPoints 'Bonus points' (Season s, Team tm) = GROUP SUM bonusPoints(Tournament t, Round r) IF played(s, t, r, tm) MATERIALIZED;

points 'Points' (Season s, Team tm) = matchPoints(s, tm) (+) bonusPoints(s, tm);

matchesUL 'Matches in European competition' (Season s, Team t) = GROUP SUM 1 IF played(t, Match m) AND season(m) = s AND isUL(m);
 
teams 'Clubs' (Season s, Country c) = GROUP SUM 1 IF matchesUL(s, Team t) AND country(t) = c;
totalPoints 'Points (total)' (Season s, Country c) = GROUP SUM points(s, Team t) IF country(t) = c;
points 'Points' (Season s, Country c) = trunc(NUMERIC[13,4](totalPoints(s, c)) / teams(s, c), 3);

index 'Index' (Season s) = PARTITION SUM 1 IF s IS Season ORDER DESC s;
rating 'Rating' (Country c) = GROUP SUM points(Season s, c) IF index(s) <= 5;

matches (Tournament t, Round r) = GROUP SUM 1 IF tournament(Match m) = t AND round(m) = r; 

EXTEND FORM tournament
    OBJECTS r = Round
    PROPERTIES name(r) READONLY, matchCoeff(r), bonusPoints(o, r)
    FILTERS matches(o, r)
;
 
FORM countryCoefficientUEFA 'UEFA country odds'
    OBJECTS s = Season
    FILTERS index(s) <= 5
    
    OBJECTS c = Country
    PROPERTIES(c) READONLY name, rating
    PROPERTIES(s, c) COLUMNS (s) points HEADER 'Points: ' + name(s), teams HEADER 'Clubs: ' + name(s)
    
    OBJECTS t = Team
    PROPERTIES(t) READONLY nameCountry, name 
    PROPERTIES(s,t) COLUMNS (s) HEADER name(s) points BACKGROUND matchesUL(s, t)
    FILTERGROUP country
        FILTER 'By country' country(t) = c DEFAULT
        
    OBJECTS m = Match
    PROPERTIES(m) READONLY dateTime, nameTournament, nameSeason, nameRound, 
                           nameHomeTeam, 
                           goalsHome BACKGROUND goalsHome(m) > goalsAway(m), 
                           goalsAway BACKGROUND goalsHome(m) < goalsAway(m), 
                           nameAwayTeam
    FILTERS played(t, m)
    ORDERS dateTime(m) DESC
;

DESIGN countryCoefficientUEFA {
    OBJECTS {
        NEW countryDetails {
            type = SPLITH;
            fill = 0.5;
            MOVE BOX(t);
            MOVE BOX(m);
        }
    }

}

NAVIGATOR {
    NEW countryCoefficientUEFA;
}